// Id: HostsByTrafficFromIPMost
// DisplayName: Hosts Receiving Most Amount of Data from IP
// Description: Hosts Receiving Most Amount of Data from IP during the range of -1d and +1d
// DataConnector: ; DataTypes: #WireData
// InputEntityType: IP
// InputFields: [[Address]]
// OutputEntityTypes: [Host]
// QueryPeriodBefore: 1d
// QueryPeriodAfter: 1d
// Tactics: #Exfiltration, #CommandAndControl, #Collection

let HostsReceivingDatafromIP = (v_IP_Address:string){
	WireData
	| parse Computer with HostName '.' Host_DnsDomain
	| where SessionState == "Disconnected" 
	| where RemoteIP == v_IP_Address
    | extend Host_HostName = iff(Computer has '.', HostName, Computer)
	| summarize Host_Aux_BytesReceived = sum(ReceivedBytes), make_set(LocalIP) by Host_HostName, Host_DnsDomain
	| top 10 by Host_Aux_BytesReceived desc nulls last
};
HostsReceivingDatafromIP('<Address>')